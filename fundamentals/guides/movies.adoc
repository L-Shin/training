= My first movie app

== My first movie app

We're going to build the queries for a little movie application using a built in dataset that comes with Neo4j.
We'll introduce Cypher constructs as our queries require them.

Before we get started make sure you've imported the data:

[source, cypher]
----
:play movies
----

If you've already run it, don't run it again or you'll end up with all the data imported twice!

== Exploring the dataset

Let's have a quick look at what we've imported.

Click on the image:{img}/top_icon.png[] icon on the left hand side.
You should see 3 headings: `Node labels`, `Relationship types`, and `Property keys`.

Try clicking on the different items under each of the headings.
Each of them will execute a query and show the results in a query pane on the right hand side.

== Finding an actor/actress

Now we're going to start building out the queries for our movies app.
Let's start with a user story and work from there.

[verse]
____
As a movie enthusiast
I want to view the profile page for my favourite actor/actress
So that I can learn everything there is to know about them
____

To meet this requirement we'll first need to write a query to find a specific actor or actress in the database.

== Finding an actor/actress

We use the `MATCH` clause to find a pattern in the database.
In this case the required pattern is relatively simple.

Run the following query:

[source, cypher]
----
MATCH (person:Person)
WHERE person.name = "Tom Hanks"
RETURN person
----

This query:

* scans through all nodes which have the label `Person`
* filters those which have the value `Tom Hanks` for the `name` property
* returns any matching nodes

Try changing the query to find someone else.

== Exercise: Finding an actor/actress

Can you work out the syntactic sugar for looking up a person without using a `WHERE` clause?

_Hint_ The link:https://neo4j.com/docs/developer-manual/current/cypher/#query-read[Cypher query language section] of the developer manual may come in handy.

== Answer: Finding an actor/actress (syntactic sugar version)

The following query is equivalent:

[source, cypher]
----
MATCH (person:Person {name: "Tom Hanks"})
RETURN person
----

== Find the movies an actor/actress acted in

So far we've written a query that returns the name and year of birth of a particular actor/actress.
To build out the profile page further it'd be good to show a list of movies that they acted in.

We can write the following query to find the movies that Tom Hanks acted in:

[source, cypher]
----
MATCH (person:Person {name: "Tom Hanks"})-[:ACTED_IN]->(movie)
RETURN movie
----

The first bit of this query is the same as the previous one but once we've found the `Tom Hanks` node we traverse any outgoing `ACTED_IN` relationships to find the movies he acted in.

== Exercise: Find the movies an actor/actress directed

Can you write a query to find the movies that Tom Hanks directed?

== Answer: Find the movies an actor/actress directed

[source, cypher]
----
MATCH (person:Person {name: "Tom Hanks"})-[:DIRECTED]->(movie)
RETURN movie
----

== Find the movies and actor/actress has acted in or directed

Hopefully that was easy enough.
If want to find the movies that he's acted in or directed we can tweak the query like this:

[source, cypher]
----
MATCH (person:Person {name: "Tom Hanks"})-[role:DIRECTED|:ACTED_IN]->(movie)
RETURN movie, TYPE(role) AS type
----

This query gets us the results we wanted, but we have multiple rows for movies that he's acted in and directed.
It'd be good to get one row per movie and have a list containing the `ACTED_IN` and `DIRECTED` relationships.

== Grouping by movie

Look at the slides for a quick explanation about how grouping works in Cypher.

image::{img}/slides.jpg[]

== Group by movie

We can write the following query to return one row per movie Tom Hanks acted in or directed:

[source, cypher]
----
MATCH (person:Person {name: "Tom Hanks"})-[role:DIRECTED|:ACTED_IN]->(movie)
RETURN movie, COLLECT(TYPE(role)) AS roles
ORDER BY SIZE(roles) DESC
----

We can use the `SIZE` function to work out how many items are in a collection.
In this case we're using is so we show the movies that Tom Hanks acted in and directed first.

== Exercise: Group by release year.

The next requirement for our movie app is to show the movies an actor/actress has acted in or directed in chronological order grouped by `releaseYear`.

Can you write that query?

== Answer: Group by release year.

[source, cypher]
----
MATCH (person:Person {name: "Tom Hanks"})-[role:DIRECTED|:ACTED_IN]->(movie)
RETURN movie.releaseYear, COLLECT(movie.title) AS movies
ORDER BY movie.releaseYear
----

You may have noticed that we have some duplicate movies in 1996 and 2011.
Those are the years that Tom Hanks acted in movies that he also directed.

We can fix the duplicate issue by using the `DISTINCT` keyword:

[source, cypher]
----
MATCH (person:Person {name: "Tom Hanks"})-[role:DIRECTED|:ACTED_IN]->(movie)
RETURN movie.releaseYear, COLLECT(DISTINCT movie.title) AS movies
ORDER BY movie.releaseYear
----

== Filtering patterns

All the queries we've written have used the `MATCH` clause to find new bits of data, but sometimes we'll want to filter things out.

Our next requirement is to build a list of the recent movies that a person has acted in.

Let's quickly introduce the `WHERE` clause:

image::{img}/slides.jpg[]

== Finding recent movies

The following query only returns movies released this century:

[source, cypher]
----
MATCH (person:Person {name: "Tom Hanks"})-[role:DIRECTED|:ACTED_IN]->(movie)
WHERE movie.releaseYear > 2000
RETURN movie
----

Try changing the query to find the movies released in the year 2000 or before the year 2000.

== Paths, paths, are everywhere

Our next requirement is to return the people that Tom Hanks has worked with in a format that we could feed into a visualisation library.
To do this we're going to write a query that returns a set of paths that could be reconstructed into a visualisation:

image::{img}/slides.jpg[]

== Finding co-actor paths

We can write the following query to find Tom Hanks' co-actors and the movies that they appeared in together:

[source, cypher]
----
MATCH path = (person:Person {name: "Tom Hanks"})-[:DIRECTED|:ACTED_IN]->()<-[:DIRECTED|:ACTED_IN]-()
RETURN path
----

This time we're returning the full path rather than its components as we have in previous queries.

If we wanted to find the `(Person)-->(Movie)<--(Person)` triples in each path we can use the `NODES` function:

[source, cypher]
----
MATCH path = (person:Person {name: "Tom Hanks"})-[:DIRECTED|:ACTED_IN]->()<-[:DIRECTED|:ACTED_IN]-()
RETURN NODES(path) AS triples
----

There's also a `RELS` function which you can use to extract the relationships from a path.

== Exercise: Similar actors/actresses

We have some space on the side bar of the profile page to show 5 similar actors/actresses.
We'll take similar to mean that they've appeared in the most movies with the actor/actress in question.

Can you write a query to find the most similar actors/actresses to `Halle Berry`?

== Answer: Similar actors/actresses

[source, cypher]
----
MATCH (person:Person {name: "Halle Berry"})-[:DIRECTED|:ACTED_IN]->()<-[:DIRECTED|:ACTED_IN]-(coactor)
RETURN coactor, COUNT(*) AS times
ORDER BY times DESC
LIMIT 5
----

== How are two people connected?

Our product owner is happy with the profile page now but we've had requests to introduce a new page where the user can type in the names of two people and find out how they're connected.
Let's quickly go over shortest path queries:

image::{img}/slides.jpg[]

== Shortest path from <x> to <y>
