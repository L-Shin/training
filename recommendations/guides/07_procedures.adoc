= Procedures
include::include.adoc[]

== Procedures

Procedures are a new feature in Neo4j 3.x which allow you to write custom code which can be invoked directly from Cypher.

You can read more details in the link:http://neo4j.com/docs/developer-manual/current/#procedures[documentation].

// image::{img}/slides.jpg[]

== Built in procedures

There are some built in procedures.
You can see what they are by running the following command:

[source, cypher]
----
CALL dbms.procedures()
----

Try running some of the procedures listed on the page.
Do they work as you expected?

== Using a procedure's output in a query

We can use the output of procedures in a query as well as passing the output of a query into a procedure.
For example if we want to get the list of labels in the database in alphabetical order we could write the following query:

[source, cypher]
----
CALL db.labels() YIELD label AS label
RETURN label
ORDER BY label
----

All the built in procedures are used to introspect the database but we can also write our own procedures which can do whatever we want!

== Installing apoc (Awesome procedures)

The link:https://github.com/neo4j-contrib/neo4j-apoc-procedures[APOC] procedure library contains lots of useful procedures that we can use in our recommendations queries.

First follow these instructions to get `apoc` installed on your local instance of Neo4j:

* Neo4j Sandbox comes pre-installed with APOC
* Neo4j Desktop has an single-click installation under "Plugins" in the "Manage" section

If you use your own installation of Neo4j then grab a USB stick from one of the trainers or download the latest, fitting version of apoc from link:https://github.com/neo4j-contrib/neo4j-apoc-procedures/releases/latest[]

* Copy `apoc*.jar` into your `plugins` folder wherever you have installed Neo4j.

* Restart Neo4j

== Check apoc installed correctly

If you run the following command you can see which additional procedures are now available to us:

[source,cypher]
----
CALL dbms.procedures() YIELD name, signature, description
WHERE name STARTS WITH "apoc"
RETURN name, description, signature
----

If you don't see any rows ask your trainer for help.

You can see in the signatures which parameters a procedure takes and which columns it returns.

A quick variant of this call is:

[source,cypher]
----
CALL apoc.help("date")
----

Let's give the procedures a try!

== Formatting timestamps

A common task is to parse and format timestamps in a human friendly way and we have just the function for that in `apoc`.
We can use it to find out the date and time of the last 5 events in our dataset:

[source,cypher]
----
MATCH (venue)<-[:VENUE]-(event:Event)<-[:HOSTED_EVENT]-(group:Group)
WHERE event.time < timestamp()
WITH event, venue, group
ORDER BY event.time DESC
LIMIT 5
WITH event, group, venue, apoc.date.format(event.time, 'ms') as dateTime
RETURN event.name, group.name, venue.name, dateTime
----

This is a user defined function which returns a string value.

== Importing JSON documents

One of the procedures that we should have seen in our list is `apoc.load.json`.
You can check that you've got it by running the following command:

[source, cypher]
----
CALL apoc.help("json")
----

This procedure allows us to consume JSON documents directly into Neo4j and import data straight into Neo4j without the CSV transformation step.

We're going to use this procedure to import some more data from Meetup, this time in JSON format.

== Importing JSON from the meetup.com API

The meetup.com API for photos is accessible without needing a key so we're going to import the meta data for photos posted to a Meetup group.

Run the following query to find the `urlname` for some groups:

[source, cypher]
----
MATCH (group:Group)
RETURN group.urlname
ORDER BY rand()
LIMIT 10
----

Let's pick one as parameter

[source, cypher]
----
:param group_url:graphdb-london
----

First let's see what photos have been posted:

[source, cypher,subs=attributes,none]
----
CALL apoc.load.json("https://api.meetup.com/"+$group_url+"/photos?photo-host=public")
YIELD value AS document
WITH document WHERE EXISTS(document.photo_album.event.id)
RETURN document.link AS link,
       document.created AS time,
       document.id AS photoId,
       document.member.id as memberId,
       document.photo_album.event.id AS eventId
----

ifdef::env-training[]

== Exercise: Import photos meta data

* Import the photo meta data into the graph.

We probably want to create this pattern `+(member)-[:POSTED]->(photo)-[:EVENT]->(event)+` although you are of course free to create whatever you like!

== Answers ahead !

image::{img}/slides.jpg[]

endif::env-training[]
ifndef::env-training[]

include::07_photos_answers.adoc[lines=4..-1]

endif::env-training[]

== Next Step

In the next couple of sections we'll use these procedures to extend the layered event recommendation we've been building up over the last few sections.

// pass:a[<a play-topic='{guides}/08_latent_social_graph.html'>Latent Social Graph</a>]
