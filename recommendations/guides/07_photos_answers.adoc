= Import photos meta data answers
include::include.adoc[]

== Exercise: Import photos meta data

* Import the photo meta data into the graph.

We probably want to create this pattern `(member)-[:POSTED_PHOTO]->(photo)-[:POSTED_IN_EVENT]->(event)` although you are of course free to create whatever you like!

== Import photos meta data

[source,cypher,subs=attributes,none]
----
CREATE CONSTRAINT ON (p:Photo) ASSERT p.id IS UNIQUE
----

[source,cypher,subs=attributes,none]
----
CALL apoc.load.json("https://api.meetup.com/{neo4j-meetup-id}/photos?&sign=true&photo-host=public")
YIELD value AS document

WITH document WHERE document.photo_album.event.id IS NOT NULL

MATCH (event:Event {id: document.photo_album.event.id})
OPTIONAL MATCH (member:Member {id: tostring(document.member.id)})

MERGE (photo:Photo {id: document.id})
ON CREATE SET photo.link = document.link, photo.created = document.created

MERGE (photo)-[:POSTED_IN_EVENT]->(event)

WITH photo, member WHERE  member is NOT NULL

MERGE (member)-[:POSTED_PHOTO]->(photo)
----
