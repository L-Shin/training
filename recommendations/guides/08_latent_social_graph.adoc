= Latent Social Graph
include::include.adoc[]

== Find the social graph lurking within

The social graph is a common feature of a lot of modern web applications.
Typically that social graph is imported from a 3rd party system e.g. Twitter followers or Facebook friends.

In this dataset there is actually a latent social graph hiding beneath the data we've looked at so far.
RSVPs give us a reasonable idea of which people may know each other based on common meetups they've attended.

== Finding people that I know

We can write the following query to see which people on the Meetup scene that I may know:

include::form.adoc[tags=name]

[source,cypher,subs=attributes]
----
MATCH (me:Member)-[:RSVPD]->()<-[:RSVPD]-(otherPerson)
WHERE me.name CONTAINS {name}
WITH otherPerson, COUNT(*) AS commonEvents
ORDER BY commonEvents DESC
LIMIT 10
RETURN otherPerson.name, commonEvents
----

== Visualizing the Social Graph

We can also visualize the social graph between our members.
We'll use the `apoc.create.vRelationship` function for that, which returns a virtual relationship that's not part of the graph.

Remember to disable the auto-completion for relationships in Neo4j Browser.

[source,cypher,subs=attributes]
----
MATCH (m:Member)-[:RSVPD]->()<-[:RSVPD]-(otherPerson)
WHERE size( (m)-[:RSVPD]->() ) >= 7 and size( (otherPerson)-[:RSVPD]->() ) >= 7
WITH m, otherPerson, COUNT(*) AS commonEvents
WHERE commonEvents >= 5
RETURN m, collect([otherPerson, apoc.create.vRelationship(m, 'FRIENDS', {commonEvents:commonEvents}, otherPerson)])[0..3] as related
ORDER BY rand() LIMIT 100
----

== Materialising the latent social graph

In this next section we're going to materialise this inferred social network and create a `FRIENDS` relationship between people who have been to 7 or more of the same events.

We'll have to run a graph global query to materialise this inferred social network so we're going to process members in batches.

First we'll add a `Process` label to active members so we know which ones we've still got left to process in the next step.
Run the following query.

[source,cypher]
----
MATCH (m:Member) WHERE size( (m)-[:RSVPD]->() ) >= 7
SET m:Process
RETURN count(*)
----

So we should have just a few thousand members to process.

== Transaction State

image::{img}/slides.jpg[]

== Materialising the latent social graph

Let's try processing just the first 500 of those members.
Run the following query:

[source,cypher]
----
MATCH (m1:Member:Process) WITH m1 LIMIT 500
REMOVE m1:Process
WITH m1
MATCH (m1)-[:RSVPD]->(event)<-[:RSVPD]-(m2)

WITH m1, m2, COUNT(*) AS times
WHERE times >= 5

MERGE (m1)-[friends:FRIENDS]-(m2) SET friends.count = times
RETURN count(*);
----

You should see that quite a number of relationships have been created.

It would be nice if we could execute that query multiple times until all members have been processed.

== Materialising the latent social graph

The `apoc.periodic.commit(statement,params)` stored procedure allows us to do just that.
Run the following query but be patient, it's got a lot of friendships to make!

[source,cypher]
----
call apoc.periodic.iterate("
  MATCH (m1:Member:Process) RETURN m1
","
  REMOVE m1:Process 
  WITH m1
  MATCH (m1)-[:RSVPD]->(event)<-[:RSVPD]-(m2)
  WITH m1, m2, COUNT(*) AS times WHERE times >= 5
  MERGE (m1)-[friends:FRIENDS]-(m2) SET friends.count = times
",{batchSize:500,iterateList:true})
----

This query will probably run for a couple of minutes, creating a lot of `FRIENDS` relationships.

Run the following query to check that they've been created:

[source,cypher]
----
MATCH ()-[:FRIENDS]-() RETURN COUNT(*)
----

ifdef::env-training[]

== Exercise: Adding friends to the recommendations query

Update the events recommendation query to:

* count how many of our friends have `RSVPD` for an event
* return the names of those friends as well

== Do we have friends?

image::{img}/slides.jpg[]

endif::env-training[]
ifndef::env-training[]

include::08_latent_answers.adoc[lines=4..-1]

endif::env-training[]

== Next Step
In our next section we're going to apply some more sophisticated scoring functions to the recommendation queries that we've built so far.

// pass:a[<a play-topic='{guides}/09_scoring.html'>Scoring</a>]
