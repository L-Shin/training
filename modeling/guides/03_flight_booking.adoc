= Flight booking
:icons: font

== Flight booking

In this section we're going to write some queries from the perspective of a frequent traveller trying to find flights to book.

[verse]
____
As a frequent traveller
I want to find flights from <origin> to <destination> on <date>
So that I can book my business flight
____

Before we write those queries let's import some more data.

== Import more flights

We initially loaded 10,000 flights.
That was a fun initial dataset to play with, but now that we've got a model we're happy let's load in a bit more data.

`flights_50k.csv` contains 50,000 flights.
We can run the following query to import those flights:

[source,cypher]
----
LOAD CSV WITH HEADERS FROM "{csv-url}flights_50k.csv" AS row
MERGE (origin:Airport {code: row.Origin})
MERGE (destination:Airport {code: row.Dest})
MERGE (newFlight:Flight { id: row.UniqueCarrier + row.FlightNum + "_" + row.Year + "-" + row.Month + "-" + row.DayofMonth + "_" + row.Origin + "_" + row.Dest }   )
ON CREATE SET newFlight.year = TOINT(row.Year),
              newFlight.month = TOINT(row.Month),
              newFlight.day = TOINT(row.DayofMonth),
              newFlight.airline = row.UniqueCarrier,
              newFlight.number = row.FlightNum,
              newFlight.departure = row.Dest,
              newFlight.arrival = row.UniqueCarrier
MERGE (origin)<-[:ORIGIN]-(newFlight)
MERGE (newFlight)-[:DESTINATION]->(destination)
----

Now it's time to write a query to find available flights between two airports on a specific date.

== Finding flights to book

Let's find all the flights going from Los Angeles (`LAS`) to Chicago Midway International (`MDW`) on the 30th January.
Run the following query:

[source, cypher]
----
MATCH path = (origin:Airport {code: "LAS"})<-[:ORIGIN]-(flight:Flight)-[:DESTINATION]->(destination:Airport {code: "MDW"})
WHERE flight.year = 2008 AND flight.month = 1 AND flight.day = 30
RETURN path
----

This returns quite quickly but try prefixing it with `PROFILE`.
What do you notice?

== Profiling the finding flights to book query

The query starts by using an index to find `MDW` but then has to traverse all incoming `DESTINATION` relationships and check 3 properties on the `:Flight` nodes on the other side.
The more flights an airport has the more we'll have to scan through, and since we're only working with 10,000 flights we should probably find a better way to model our data before importing any more rows.

Can you think of a way that we can change our model to avoid doing all these property lookups?

== Profiling the finding flights to book query

We can profile the query by prefixing it with the `PROFILE` keyword:

[source, cypher]
----
PROFILE
MATCH path = (origin:Airport {code: "LAS"})<-[:ORIGIN]-(flight:Flight)-[:DESTINATION]->(destination:Airport {code: "MDW"})
WHERE flight.year = 2008 AND flight.month = 1 AND flight.day = 30
RETURN path
----

== Introducing `AirportDay`

One way that we can tweak our model to be more aligned with our queries is by introducing the concept of an `AirportDay`

image::{img}/slides.jpg[]

== Exercise: Introducing Airport Day

We want to introduce `:AirportDay` nodes so that we don't have to scan through all the flights going from an airport when we're only interested in a subset of them.

Try and write a query to evolve our current model to include this new concept.

image::{img}/airport_day.png[]

== Click through for the answers

If you really want to see them...

== Answer: Introducing Airport Day

Before we create anything let's put a unique constraint on `:AirportDay` so we don't create any duplicates:

[source, cypher]
----
CREATE CONSTRAINT ON (airportDay:AirportDay)
ASSERT airportDay.id IS UNIQUE
----

We'll use the combination of origin and the flight date as our unique key for an `:AirportDay`:

[source, cypher]
----
MATCH (origin:Airport)<-[:ORIGIN]-(flight:Flight)-[:DESTINATION]->(destination:Airport)
MERGE (originAirportDay:AirportDay {id: origin.code + "_" + flight.year + "-" + flight.month + "-" + flight.day})
ON CREATE SET originAirportDay.date = flight.year + "-" + flight.month + "-" + flight.day
MERGE (destinationAirportDay:AirportDay {id: destination.code + "_" + flight.year + "-" + flight.month + "-" + flight.day})
ON CREATE SET destinationAirportDay.date = flight.year + "-" + flight.month + "-" + flight.day
MERGE (origin)-[:HAS_DAY]->(originAirportDay)
MERGE (originAirportDay)<-[:ORIGIN]-(flight)
MERGE (flight)-[:DESTINATION]-(destinationAirportDay)
----

== Find flights to book

Now let's try finding those flights between Los Angeles and Chicago Midway International again.
To recap, this was our original query:

[source, cypher]
----
MATCH path = (origin:Airport {code: "LAS"})<-[:ORIGIN]-(flight:Flight)-[:DESTINATION]->(destination:Airport {code: "MDW"})
WHERE flight.year = 2008 AND flight.month = 1 AND flight.day = 30
RETURN path
----

This is the equivalent query which makes use of `:AirportDay`:

[source, cypher]
----
MATCH (origin:Airport {code: "LAS"})-[:HAS_DAY]->(:AirportDay {date: "2008-1-30"})<-[:ORIGIN]-(flight:Flight),
      (flight)-[:DESTINATION]->(:AirportDay {date: "2008-1-30"})<-[:HAS_DAY]-(destination:Airport {code: "MDW"})
RETURN *
----

Try profiling the queries.
What do you notice?

== Next Step

Depending on how powerful your laptop is, you might have noticed that our refactoring query is getting a bit slow.
In the next section we'll look at how we can process refactoring queries in batches.

pass:a[<a play-topic='{guides}/04_refactoring_large_graphs.html'>Refactoring large graphs</a>]
