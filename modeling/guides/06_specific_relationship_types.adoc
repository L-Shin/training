= Specific Relationship Types
:icons: font

== Specific Relationship Types

If we load in all the data for one year we'll have to scan through ~365 `HAS_DAY` relationships for each airport and check the `date` property for the `:AirportDay` node at the end of each relationship.
If we load in 10 years that goes up to 3,650 relationships.

So it's not an optimal model just yet but what can we do about it?

== Making our relationships more specific

Neo4j is optimized for searching by unique relationship types and in this case the date of a flight provides that uniqueness.


We can refactor our model to change the `HAS_DAY` relationship to be the date of the flight instead e.g. `2008_1_30`

This is what our new model will look like:

image::{img}/specific_relationship.jpg[]

== Refactoring to specific relationship type

Creating relationships with dynamic names isn't possible using pure Cypher but `apoc` contains a function that we can use.

[cypher, source]
----
CALL apoc.help("apoc.refactor.setType")
----

We want to replace all the `HAS_DAY` relationships with relationships whose type comes from the `date` property on the `AirportDay` nodes:

[cypher, source]
----
MATCH path = (origin:Airport)-[hasDay:HAS_DAY]->(ad:AirportDay)
RETURN path
LIMIT 1
----

== Refactoring to specific relationship type

We can run the following query to replace all the `HAS_DAY` relationships:

[cypher, source]
----
MATCH (origin:Airport)-[hasDay:HAS_DAY]->(ad:AirportDay)
CALL apoc.refactor.setType(hasDay, ad.date) YIELD input, output, error
RETURN COUNT(*)
----

Now let's rewrite our query that finds all the flights between Los Angeles and Chicago Midtown International:

[source, cypher]
----
MATCH (origin:Airport {code: "LAS"})-[:`2008-1-30`]->(:AirportDay)<-[:ORIGIN]-(flight:Flight),
      (flight)-[:DESTINATION]->(:AirportDay)<-[:`2008-1-30`]-(destination:Airport {code: "MDW"})
RETURN *
----

[source, cypher]
----
PROFILE
MATCH (origin:Airport {code: "LAS"})-[:`2008-1-30`]->(:AirportDay)<-[:ORIGIN]-(flight:Flight),
      (flight)-[:DESTINATION]->(:AirportDay)<-[:`2008-1-30`]-(destination:Airport {code: "MDW"})
USING INDEX origin:Airport(code)
USING INDEX destination:Airport(code)
RETURN *
----


== Refactoring to specific relationship types

What do you think?
Should we bother with this careful refactoring or should we just load all our data from scratch?
