= Multiple Models
:icons: font

== Multiple Models

During some of our refactoring queries we didn't delete the old model.
Let's look at that decision in more detail.

image::{img}/slides.jpg[]

== General or Specific or Both

In the specific relationship types guide we introduced data based relationships between `:Airport` and `:AirportDay` nodes.
We saw that this refactoring gave us a performance boost when trying to find the flights between two airports on a particular day.

[source, cypher]
----
PROFILE
MATCH (origin:Airport {code: "LAS"})-[:`2008-1-3`]->(:AirportDay)<-[:ORIGIN]-(flight:Flight),
      (flight)-[:DESTINATION]->(:AirportDay)<-[:`2008-1-3`]-(destination:Airport {code: "MDW"})
RETURN *
----

vs

[source, cypher]
----
PROFILE
MATCH (origin:Airport {code: "LAS"})-[:HAS_DAY]->(:AirportDay {date: "2008-1-3"})<-[:ORIGIN]-(flight:Flight),
      (flight)-[:DESTINATION]->(:AirportDay {date: "2008-1-3"})<-[:HAS_DAY]-(destination:Airport {code: "MDW"})
RETURN *
----

If that's the only date based query that we want to run then it'd make sense to delete the `HAS_DAY` relationships and just go with the more specific relationship type.

== Find flights by year and month

But what if a new requirement comes up to allow our users to search all flights that happened in a particular month?

This would be very easy to write with the `HAS_DAY` relationship:

[source, cypher]
----
MATCH (origin:Airport {code: "LAS"})-[:HAS_DAY]->(originDay:AirportDay),
      (originDay)<-[:ORIGIN]-(flight:Flight)
WHERE originDay.date STARTS WITH "2008-1"
RETURN flight.date, flight.number, flight.airline, flight.departure, flight.arrival
ORDER BY flight.date, flight.departure
----

With the data based relationships we'd need to specify each of them individually:

[source, cypher]
----
MATCH (origin:Airport {code: "LAS"})
       -[:`2008-1-3`|:`2008-1-4`|:`2008-1-5`|:`2008-1-6`]->(originDay:AirportDay),
      (originDay)<-[:ORIGIN]-(flight:Flight)
RETURN flight.date, flight.number, flight.airline, flight.departure, flight.arrival
ORDER BY flight.date, flight.departure
----

The above query only finds the flights on 4 of the days in January 2008.
We'd need to add a few more relationship types to cover the whole month.

In summary, the date based relationship type works really well when we want to find flights on a specific date but becomes a bit unwieldy when we want to search more broadly.
So should we keep both relationship types?

== Keep both?

If we keep both relationships it seems like we get the best of both worlds.
We can have our fast specific day queries and we can easily write queries to find flights in a specific month.

The disadvantage is that we need to keep these relationships in sync from our application code.

== Archiving flights

For example, let's say we decide to archive all the flights from January 1st 2008 which means we need to delete them from our graph.

[source, cypher]
----
MATCH (airportDay:AirportDay {date: "2008-1-1"})
MATCH (airportDay)<-[general:HAS_DAY]-()
MATCH (airportDay)<-[specific:`2008-1-1`]-()
DELETE general, specific
----

We need to make sure we delete both the `HAS_DAY` and date specific relationship otherwise the data will be inconsistent depending on which relationship type we query through.

== Adding flights on new dates

We have the same problem when adding flights on a new date.
Run the following query to add flights from a different day:

[source, cypher, subs = attributes]
----
LOAD CSV WITH HEADERS FROM "{csv-url}flights_february.csv" AS row

MERGE (origin:Airport {code: row.Origin})
MERGE (destination:Airport {code: row.Dest})

MERGE (newFlight:Flight { id: row.UniqueCarrier + row.FlightNum + "_" + row.Year + "-" + row.Month + "-" + row.DayofMonth + "_" + row.Origin + "_" + row.Dest }   )
ON CREATE SET newFlight.date = TOINT(row.Year) + "-" + TOINT(row.Month) + "-" + TOINT(row.DayofMonth),
              newFlight.airline = row.UniqueCarrier,
              newFlight.number = row.FlightNum,
              newFlight.departure = TOINT(row.CRSDepTime),
              newFlight.arrival = TOINT(row.CRSArrTime)
MERGE (origin)<-[:ORIGIN]-(newFlight)
MERGE (newFlight)-[:DESTINATION]->(destination)

MERGE (originAirportDay:AirportDay {id: origin.code + "_" + newFlight.date})
ON CREATE SET originAirportDay.date = newFlight.date

MERGE (destinationAirportDay:AirportDay {id: destination.code + "_" + newFlight.date})
ON CREATE SET destinationAirportDay.date = newFlight.date

MERGE (originAirportDay)<-[:ORIGIN]-(flight)
MERGE (flight)-[:DESTINATION]-(destinationAirportDay)

MERGE (origin)-[hasDayOrigin:HAS_DAY]->(originAirportDay)
MERGE (destination)-[hasDayDestination:HAS_DAY]->(destinationAirportDay)

WITH *
UNWIND [[hasDayOrigin, originAirportDay], [hasDayDestination, destinationAirportDay]] AS pair

WITH DISTINCT pair
CALL apoc.convert.toRelationship(pair[0]) YIELD rel
WITH rel AS hasDay, pair[1] AS day

CALL apoc.create.relationship(startNode(hasDay), day.date, {}, endNode(hasDay) )
YIELD rel
RETURN COUNT(*)
----

== Do we really need both models?

At this point it's becoming quite onerous keeping both models in check.
Perhaps we can find a way to achieve our querying goals without having two models?

image::{img}/slides.jpg[]

== Deleting airports

Since we don't need airports anymore, let's get rid of them:

[source, cypher]
----
MATCH (airport:Airport)
DETACH DELETE airport
----

`DETACH DELETE` removes a node and all the relationships attached to it, which is exactly what we want to do.

== Exercise: Find flights with our new model

Try writing queries that start from `:AirportDay` to find:

* flights between `LAS` and `MDM` on `2008-1-3`
* flights going out of `LAS` on in January 2008

== Click through for the answers

If you really want to see them...

== Answer: Find flights on a particular day

[source, cypher]
----
MATCH (origin:AirportDay)<-[:ORIGIN]-(flight:Flight)-[:DESTINATION]->(destination:AirportDay)
WHERE origin.id = "LAS_2008-1-3" AND destination.id = "MDM_2008-1-3"
RETURN *
----

== Answer: Find flights by year and month

[source, cypher]
----
MATCH (originDay:AirportDay)<-[:ORIGIN]-(flight:Flight)
WHERE originDay.id STARTS WITH "LAS_2008-1"
RETURN flight.date, flight.number, flight.airline, flight.departure, flight.arrival
ORDER BY flight.date, flight.departure
----

== Next

In the next section we're going to split into groups and you'll have a chance to apply some of the things you've learnt.

pass:a[<a play-topic='{guides}/07_your_turn.html'>Your Turn</a>]
