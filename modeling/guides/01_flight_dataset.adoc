= Intro to The Flight Dataset
:icons: font

== Intro to The Flight Dataset

Welcome to the first of a set of interactive guides.
In these guides we'll evolve a dataset containing the flights between US airports in 2008.

Let's get started!

== Start with a question

When modeling a dataset it always helps to have a use case in mind.
We'll start with the following question:

[verse]
____
As a plane enthusiast
I want to know how many flights there are between <origin> and <destination>
So that I can know how busy various airports are
____

Let's have a look what data we've got to help us answer this question:

== Exploring data with `LOAD CSV`

As we saw from our brief command line exploration, we've got a lot of flights to work with.
We could use command line tools to explore this further but Cypher's `LOAD CSV` command is actually perfect for exploring CSV files.

_Slides here for a LOAD CSV primer_

== Exploring data with `LOAD CSV`

We can run the following query to see what data we've got to work with:

[source,cypher]
----
LOAD CSV WITH HEADERS FROM "file:///2008.csv" AS row
RETURN row
LIMIT 5
----

This query:

* loads the file `2008.csv` from your `import` directory
* iterates over the file, referring to each line as the variable `row`
* and returns the first 5 lines in the file

We've got lots of different fields but the ones that will be helpful for answering our question are: `Origin`, `Dest`, and `FlightNum`.

== Modeling flights and airports

Before we use `LOAD CSV` to import any data we need to know what model we want to create.
The following should suffice:

image::{img}/initial.jpg[]

We should do a quite primer here on the building blocks of the model.

== Importing flights and airports

While we're working out the appropriate model for our dataset it's much easier to work with a subset of the data so that we can iterate quickly.
We'll work with the first 10,000 rows of the CSV file.

Run the following query to create nodes and relationships for these flights:

[source,cypher]
----
LOAD CSV WITH HEADERS FROM "file:///2008.csv" AS row
WITH row LIMIT 10000
MERGE (origin:Airport {code: row.Origin})
MERGE (destination:Airport {code: row.Dest})
MERGE (origin)-[flight:FLIGHT {airline: row.UniqueCarrier,
                               flightNumber: row.FlightNum,
                               year: TOINT(row.Year),
                               month: TOINT(row.Month),
                               day: TOINT(row.DayofMonth)}]->(destination)
ON CREATE SET flight.departure = TOINT(row.CRSDepTime), flight.arrival = TOINT(row.CRSArrTime)
----

This query:

* takes the first 10,000 rows in the CSV file
* creates nodes with the `Airport` label for the origin and destination airports if they don't already exist
* creates a `FLIGHT` relationship between origin and destination airports for each row in the file

By default properties will be stored as strings.
We know that `year`, `month`, and `day` are actually numeric values so we'll coerce them using the link:/docs[`TOINT`] function.

Now we're ready to start querying the data.

== Finding the most popular airports

We can see some of what we've imported by writing the following query, which finds the airports with the most incoming and outgoing flights.

[source, cypher]
----
MATCH (a:Airport)
WITH a, SIZE((a)-[:FLIGHT]->()) AS outgoing, SIZE((a)<-[:FLIGHT]-()) AS incoming
RETURN a.code, outgoing, incoming
ORDER BY outgoing DESC
----

This query:

* finds every node with the `Airport` label
* uses the `SIZE` function to count the number of incoming and outgoing relationship of type `FLIGHT` for each `Airport` node
* returns the `code` property on the `Airport` nodes and the `outgoing` and `incoming` counts in descending order by `outgoing`

== Exercise: Finding flights

Now it's your turn!

* Find all the flights for flight number `WN 1016`
* Find all the flights in January 2008 that go to Chicago (`CHN`)



== Answer: Find all the flights for flight number `WN 1016`

[source, cypher]
----
MATCH  (origin:Airport)-[flight:FLIGHT]->(destination:Airport)
WHERE flight.airline = "WN" AND flight.flightNumber = "1016"
RETURN origin.code, destination.code, flight.year, flight.month, flight.day, flight.departure, flight.arrival
ORDER BY flight.year, flight.month, flight.day, flight.departure
----

== Next Step

In the next section we're going to refactor the model to introduce `Flight` as a first class concept to make this type of query easier to write.

pass:a[<a play-topic='{guides}/02_flight.html'>Flight as a first class citizen</a>]
