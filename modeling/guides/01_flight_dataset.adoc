= Intro to The Flight Dataset

== Intro to The Flight Dataset

Welcome to the first of a set of interactive guides.
In these guides we'll evolve a dataset containing the flights between US airports in 2008.

Let's get started!

== Start with a question

When modeling a dataset it always helps to have a use case in mind.
We'll start with the following question:

[verse]
____
As a plane enthusiast
I want to know how many flights there are between <origin> and <destination>
So that I can know how busy various airports are
____

Let's have a look what data we've got to help us answer this question:

== Exploring data with `LOAD CSV`

As we saw from our brief command line exploration, we've got a lot of flights to work with.
We could use command line tools to explore this further but Cypher's `LOAD CSV` command is actually perfect for exploring CSV files.

image::{img}/slides.jpg[]

== Exploring data with `LOAD CSV`

While we're working out the appropriate model for our dataset it's much easier to work with a subset of the data so that we can iterate quickly.
A smaller dataset containing the first 10,000 flights lives in `flights_initial.csv`.

We can run the following query to see what data we've got to work with:

[source,cypher,subs=attributes]
----
LOAD CSV WITH HEADERS FROM "{csv-url}flights_initial.csv" AS row
RETURN row
LIMIT 5
----

This query:

* loads the file `flights_initial.csv`
* iterates over the file, referring to each line as the variable `row`
* and returns the first 5 lines in the file

We've got lots of different fields but the ones that will be helpful for answering our question are: `Origin`, `Dest`, and `FlightNum`.

== Modeling flights and airports

Before we use `LOAD CSV` to import any data we need to know what model we want to create.
The following should suffice:

image::{img}/initial.png[width="100%"]

// TODO We should do a quite primer here on the building blocks of the model.

== Importing flights and airports

Run the following query to create nodes and relationships for these flights:

[source,cypher,subs=attributes]
----
LOAD CSV WITH HEADERS FROM "{csv-url}flights_initial.csv" AS row
MERGE (origin:Airport {code: row.Origin})
MERGE (destination:Airport {code: row.Dest})
MERGE (origin)-[flight:FLIGHT {airline: row.UniqueCarrier,
                               flightNumber: row.FlightNum,
                               year: toInt(row.Year),
                               month: toInt(row.Month),
                               day: toInt(row.DayofMonth)}]->(destination)
ON CREATE SET flight.departure = toInt(row.CRSDepTime), flight.arrival = toInt(row.CRSArrTime)
----

This query:

* iterates through each row in the file
* creates nodes with the `Airport` label for the origin and destination airports if they don't already exist
* creates a `FLIGHT` relationship between origin and destination airports for each row in the file

By default properties will be stored as strings.
We know that `year`, `month`, and `day` are actually numeric values so we'll convert them using the link:/docs[`toInt`] function.

Now we're ready to start querying the data.

== Finding the most popular airports

We can see some of what we've imported by writing the following query, which finds the airports with the most outgoing flights.

[source, cypher]
----
MATCH (a:Airport)
WITH a, size( (a)-[:FLIGHT]->() ) AS outgoing
RETURN a.code, outgoing
ORDER BY outgoing DESC
LIMIT 10
----

This query:

* finds every node with the `Airport` label
* uses the `size` function to count the number of outgoing relationship of type `FLIGHT` for each `Airport` node
* returns the `code` property on the `Airport` nodes and the `outgoing` count in descending order by `outgoing`
* limits the number of airports returned to 10

== Exercise: Finding flights

Now it's your turn!
Try and write queries to answer the following questions:

* Find the airports that have the most incoming flights
* Find all the flights that go to Las Vegas (`LAS`) and return ordered flight and airport details
* Find all the flights from Las Vegas (`LAS`) to Los Angeles (`LAX`) and return ordered flight and airport details

_Hint_ You'll want to refer to the link:http://neo4j.com/docs/cypher-refcard/current/[Cypher refcard] for the syntax for the second question.

== Solutions on the next pages

== Answer: Find the airports that have the most incoming flights

[source, cypher]
----
MATCH (a:Airport)
WITH a, size( ()-[:FLIGHT]->(a) ) AS incoming
RETURN a.code, incoming
ORDER BY incoming DESC
LIMIT 10
----

== Answer: Find all the flights that go to Las Vegas (`LAS`)

.Graph Result
[source, cypher]
----
MATCH  (origin:Airport)-[flight:FLIGHT]->(destination:Airport {code:'LAS'})
RETURN origin, flight, destination
----

.Returning ordered flight detail
[source, cypher]
----
MATCH  (origin:Airport)-[flight:FLIGHT]->(destination:Airport)
WHERE destination.code = "LAS"
RETURN origin.code, destination.code, flight.year, flight.month, flight.day, flight.departure, flight.arrival
ORDER BY flight.year, flight.month, flight.day, flight.departure
----

== Answer: Find all the flights from Las Vegas (`LAS`) to Los Angeles (`LAX`)

.Graph Result
[source, cypher]
----
MATCH  (origin:Airport {code:'LAS'})-[flight:FLIGHT]->(destination:Airport {code:'LAX'})
RETURN origin, flight, destination
----

.Returning ordered flight detail
[source, cypher]
----
MATCH  (origin:Airport {code: "LAX"})-[flight:FLIGHT]->(destination:Airport {code: "LAS"})
RETURN origin.code, destination.code, flight.year, flight.month, flight.day, flight.departure, flight.arrival
ORDER BY flight.year, flight.month, flight.day, flight.departure
----

== Finding specific flights

The model has worked well so far.
We've been able to find the popular airports and find the flights between pairs of airports without much trouble.

What about if we want to find all the occurrences of a specific flight?

[verse]
____
As a plane enthusiast
I want to know the schedule for <flight number>
So that I know when I'll be able to spot those planes taking off and landing
____

== Finding flight `WN 1016`

Our next query finds all the instances of flight `WN 1016`:

[source, cypher]
----
MATCH  (origin:Airport)-[flight:FLIGHT]->(destination:Airport)
WHERE flight.airline = "WN" AND flight.flightNumber = "1016"
RETURN origin.code, destination.code, flight.year, flight.month, flight.day, flight.departure, flight.arrival
ORDER BY flight.year, flight.month, flight.day, flight.departure
----

It's still reasonably quick because we only have 10,000 rows, but under the covers we're actually doing a lot of unnecessary work.

== Next Step

In the next section we're going to learn how to profile queries and we'll then refactor the model to introduce `Flight` as a first class concept.

pass:a[<a play-topic='{guides}/02_flight.html'>Flight as a first class citizen</a>]
