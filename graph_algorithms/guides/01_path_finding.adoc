= Path Finding and Graph Search Algorithms
:icons: font
:csv_url: https://gist.githubusercontent.com/lassewesth/634281cced11147432cf232a2c36e080/raw/1ed1f4fe4ca4c8092bbc8557addd1e5d87316833/eroads.csv

== Path Finding and Graph Search Algorithms

In this guide we'll learn how to use the Path Finding and Graph Search Algorithms.

Let's get started!

== E-roads dataset

We're going to use a European Roads dataset that has been http://lassewesth.blogspot.com/2018/07/the-international-e-road-network-and.html[massaged into CSV format^] thanks to Lasse Westh-Nielson.

We can use the `LOAD CSV` command to see what type of data we'll be dealing with.
Run the following query to return the first 10 rows of the CSV file:

[source, cypher,subs=attributes]
----
LOAD CSV WITH HEADERS FROM "{csv_url}" AS row
RETURN row
LIMIT 10
----

== E-roads dataset: Unique Constraint

Before we import any data we'll create a constraint on the `Place` node label so that we don't accidentally end up with duplicate nodes:

[source, cypher]
----
CREATE CONSTRAINT ON (p:Place)
ASSERT p.name IS UNIQUE
----

== E-roads dataset: Import

Now we can run the following query to import the dataset into Neo4j:

[source, cypher,subs=attributes]
----
USING PERIODIC COMMIT 1000
LOAD CSV WITH HEADERS FROM "{csv_url}" AS row

MERGE (origin:Place {name: row.origin_reference_place})
SET origin.countryCode = row.origin_country_code

MERGE (destination:Place {name: row.destination_reference_place})
SET destination.countryCode = row.destination_country_code

MERGE (origin)-[eroad:EROAD {road_number: row.road_number}]->(destination)
SET eroad.distance = toInteger(row.distance), eroad.watercrossing = row.watercrossing
----

== Shortest Path

[source, cypher]
----
MATCH p=shortestPath((aarhus:Place {name: "Århus"})-[rels:EROAD*]-(rome:Place {name: "Roma"}))
RETURN [place in nodes(p) | place.name][1..-1] AS journey,
       length(nodes(p)[1..-1]) AS intermediatePlaces,
       reduce(s = 0, r in rels | s + r.distance) AS total_distance
----


== Shortest Weighted Path


[source, cypher]
----
MATCH (aarhus:Place {name: "Århus"}), (rome:Place {name: "Roma"})
CALL algo.shortestPath.stream(aarhus, rome, "distance")
YIELD nodeId, cost
MATCH (n) WHERE id(n) = nodeId
return n.name, cost
----

== Yen's K-shortest Paths


[source, cypher]
----
MATCH (start:Place {name:"Århus"}), (end:Place {name:"Roma"})
CALL algo.kShortestPaths.stream(start, end, 5, 'distance')
YIELD index, nodeIds, path, costs
RETURN index,
       [node in algo.getNodesById(nodeIds[1..-1]) | node.name] AS via,
       reduce(acc=0.0, cost in costs | acc + cost) AS totalCost
----

== All Pairs Shortest Path

[source, cypher]
----
CALL algo.allShortestPaths.stream("distance")
YIELD sourceNodeId, targetNodeId, distance
WHERE sourceNodeId < targetNodeId
RETURN algo.getNodeById(sourceNodeId).name AS source,
       algo.getNodeById(targetNodeId).name AS target,
       distance
ORDER BY distance DESC
LIMIT 10
----


== Single Source Shortest Path

[source, cypher]
----
MATCH (n:Place {name:"London"})
CALL algo.shortestPath.deltaStepping.stream(n, "distance", 1.0)
YIELD nodeId, distance WHERE algo.isFinite(distance)
RETURN algo.getNodeById(nodeId).name AS destination, distance
ORDER BY distance
----

== Minimum Spanning Tree

[source, cypher]
----
MATCH (n:Place {name:"Amsterdam"})
CALL algo.spanningTree.minimum("Place", "EROAD", "distance", id(n),
  {write:true, writeProperty:"MINST"})
YIELD loadMillis, computeMillis, writeMillis, effectiveNodeCount
RETURN loadMillis, computeMillis, writeMillis, effectiveNodeCount
----

[source, cypher]
----
MATCH path = (p:Place {name:"Amsterdam"})-[:MINST*]-(end)
RETURN [n in nodes(path) | n.name]
----

== Random Walk

[source, cypher]
----
MATCH (source:Place {name: "Goch"})
CALL algo.randomWalk.stream(id(source), 10, 5)
YIELD nodeIds
RETURN [place in algo.getNodesById(nodeIds) | place.name] AS places
----


We can use this to try and find parts of the network that are not that resilient
e.g. maybe there's only one road in or out of a city

[source, cypher]
----
MATCH (source:Place {name: "Goch"})
CALL algo.randomWalk.stream(id(source), 10, 50000)
YIELD nodeIds
RETURN [place in algo.getNodesById(nodeIds) | place.name] AS places, count(*) AS times
ORDER BY times DESC
----


[source, cypher]
----
MATCH (p1:Place {name: "Nijmegen"})
MATCH (p2:Place {name: "Goch"})
MATCH path = (neighbor1)-[:EROAD]-(p1)-[:EROAD]-(p2)-[:EROAD]-(neighbor2)
RETURN path
----
